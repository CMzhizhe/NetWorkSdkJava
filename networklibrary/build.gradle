apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

def lib_versionCode = 1
def lib_versionName ="1.0.0"


//将源码注释打包进aar ======
//生成文档注释
task androidJavadocs(type: Javadoc) {
    failOnError = false
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)
}
//将文档打包成jar
task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set("javadoc")
    from androidJavadocs.destinationDir
}
//将源码打包
task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}
//包含文档和源码的aar
artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
//将源码注释打包进aar ====== end

android {
    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 31
        versionCode lib_versionCode
        versionName lib_versionName
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //第三方包的依赖
    compileOnly 'androidx.core:core-ktx:1.3.2'
    compileOnly 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.5.31'
    compileOnly 'io.reactivex.rxjava3:rxjava:3.1.3'
    compileOnly 'io.reactivex.rxjava3:rxandroid:3.0.0'
    compileOnly 'com.squareup.retrofit2:retrofit:2.9.0'
    compileOnly 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    compileOnly 'com.squareup.retrofit2:converter-gson:2.9.0'
    compileOnly 'com.squareup.okhttp3:okhttp:4.8.1'
    compileOnly 'com.squareup.okhttp3:logging-interceptor:4.8.1'
    compileOnly 'com.google.code.gson:gson:2.9.0'
}
repositories {
    mavenCentral()
}
